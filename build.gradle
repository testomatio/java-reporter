plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.16.0'
}

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.slf4j:slf4j-simple:2.0.16'
    implementation 'org.aspectj:aspectjweaver:1.9.22'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
    api 'io.qameta.allure:allure-testng:2.29.1'
    implementation 'org.testng:testng:7.10.2'
}

test {
    useTestNG()
}

tasks.register("createJReleaserDir") {
    doLast {
        file("build/jreleaser").mkdirs()
    }
}

tasks.named("jreleaserFullRelease").configure {
    dependsOn "createJReleaserDir"
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                app {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

                    pom {
                        group = 'io.testomat'
                        name = project.name
                        description = 'Testomatio Java Reporter - A reporting tool for Testomat.io'
                        url = 'https://github.com/testomatio/java-reporter'
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'LolikTest'
                                name = 'Oleksii Lakovych'
                                email = 'lolikla8@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git//github.com/testomatio/java-reporter'
                            developerConnection = 'scm:git:ssh://github.com/LolikTest'
                            url = 'https://github.com/testomatio/java-reporter'
                        }
                    }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}